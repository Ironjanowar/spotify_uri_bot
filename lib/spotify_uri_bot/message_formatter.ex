defmodule SpotifyUriBot.MessageFormatter do
  alias SpotifyUriBot.Model.{Track, Artist, Album, Playlist, Show, Episode}

  alias ExGram.Model.InlineQueryResultArticle
  alias ExGram.Model.InlineQueryResultAudio
  alias ExGram.Model.InputTextMessageContent

  def format(%Track{} = track) do
    """
    ğŸ¤ Artist: `#{track.artist}`
    ğŸµ Song: `#{track.name}`
    ğŸ“€ Album: `#{track.album}`
    ğŸ”— URI: `#{track.uri}`
    """ <> SpotifyUriBot.Utils.hashtags(track.genres)
  end

  def format(%Artist{} = artist) do
    """
    ğŸ¤ Artist: `#{artist.name}`
    ğŸ”— URI: `#{artist.uri}`
    """ <> SpotifyUriBot.Utils.hashtags(artist.genres)
  end

  def format(%Album{} = album) do
    """
    ğŸ¤ Artist: `#{album.artist}`
    ğŸ“€ Album: `#{album.name}`
    ğŸ“… Release date: `#{album.release_date}`
    ğŸ”— URI: `#{album.uri}`
    """ <> SpotifyUriBot.Utils.hashtags(album.genres)
  end

  def format(%Playlist{} = playlist) do
    description =
      case playlist.description do
        d when d in ["", nil] -> ""
        d -> "Description: `#{d}`"
      end

    """
    ğŸ“„ Name: `#{playlist.name}`
    ğŸ‘¤ Owner: `#{playlist.owner}`
    ğŸ”— URI: `#{playlist.uri}`
    #{description}
    """
  end

  def format(%Show{} = show) do
    """
    ğŸ“„ Name: `#{show.name}`
    ğŸ‘¤ Publisher: `#{show.publisher}`
    ğŸŒ Languages: `#{Enum.join(show.languages, ", ")}`
    #ï¸âƒ£ Number of episodes: `#{show.episodes}`
    ğŸ”— URI: `#{show.uri}`
    ğŸ“— Description:
    `#{show.description}`
    """
  end

  def format(%Episode{} = episode) do
    """
    ğŸ“„ Name: `#{episode.name}`
    ğŸ‘¤ Publisher: `#{episode.publisher}`
    ğŸŒ Languages: `#{episode.language}`
    ğŸ”— URI: `#{episode.uri}`
    ğŸ“— Description:
    `#{episode.description}`
    """
  end

  def get_inline_article(entity, opts \\ [])

  def get_inline_article(%{preview_url: _preview_url} = entity, opts) do
    {message_text, markup} = get_message_with_markup(entity)

    %InlineQueryResultAudio{
      type: "audio",
      id: entity.uri,
      title: opts[:title],
      performer: opts[:description],
      audio_url: entity.preview_url,
      input_message_content: %InputTextMessageContent{
        message_text: message_text,
        parse_mode: "Markdown"
      },
      reply_markup: markup
    }
  end

  def get_inline_article(entity, opts) do
    {message_text, markup} = get_message_with_markup(entity)

    %InlineQueryResultArticle{
      type: "article",
      id: entity.uri,
      title: opts[:title],
      input_message_content: %InputTextMessageContent{
        message_text: message_text,
        parse_mode: "Markdown"
      },
      reply_markup: markup,
      description: opts[:description] || ""
    }
  end

  def get_message_with_markup(entity) do
    message_text = format(entity)
    markup = SpotifyUriBot.Utils.generate_url_buttons(entity.href, entity.uri)
    {message_text, markup}
  end
end
